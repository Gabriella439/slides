% Namespaced De Bruijn indices
% Gabriella Gonzalez
% April 16, 2022

# Context

<link rel="stylesheet" href="https://cdn.jsdelivr.net/gh/tonsky/FiraCode@4/distr/fira_code.css">
<style>
code {
  font-family: "Fira Code";
}
</style>

This talk is a longer presentation based on a blog post I published:

* [`haskellforall.com` - Namespaced De Bruijn indices](https://www.haskellforall.com/2021/08/namespaced-de-bruijn-indices.html)

In this talk I'll spend more time focusing on:

* the motivation, history and logical progression leading up to this idea
* the user experience (as opposed to the implementation)

As the blog post notes,
[CINNI](https://www.sciencedirect.com/science/article/pii/S1571066105801252)
predates this work and presents exactly the same idea

# Overview

* **Î²-reduction**
* Name preservation
* Namespaced De Bruijn indices
* Comparison to existing approaches

# History

I first stumbled upon this idea while working on
[Morte](https://github.com/Gabriel439/Haskell-Morte-Library/issues/1), which is:

* essentially the calculus of constructions + an import system
* a proof of concept of my [internet of code](https://www.haskellforall.com/2015/05/the-internet-of-code.html) idea
* the predecessor of [Dhall][dhall]

Also, I name all of my languages after characters from [Planescape: Torment](https://en.wikipedia.org/wiki/Planescape:_Torment)

# Motivation

Both Morte and Dhall are
[total functional programming languages](https://en.wikipedia.org/wiki/Total_functional_programming)

This means all expressions can be "Î²-reduced" to a normal form.  For example:

```haskell
âŠ¢ :let List/generate = https://prelude.dhall-lang.org/List/generate.dhall

List/generate : âˆ€(n : Natural) â†’ âˆ€(a : Type) â†’ âˆ€(f : Natural â†’ a) â†’ List a

âŠ¢ List/generate 10 Bool Natural/even

[ True, False, True, False, True, False, True, False, True, False ]

âŠ¢ List/generate 10

Î»(a : Type) â†’
Î»(f : Natural â†’ a) â†’
  [ f 0, f 1, f 2, f 3, f 4, f 5, f 6, f 7, f 8, f 9 ]
```

# Î²-reduction

Î²-reduction is (sort of) the same thing as function evaluation

The Dhall REPL Î²-reduces every expression you enter in the REPL:

```haskell
âŠ¢ Natural/even 2  -- This is a "Î²-reducible expression" (a.k.a. a "Î²-redex")

True
```

The key thing to stress is that Î²-reduction even evaluates "under lambda"

```haskell
âŠ¢ Î»(a : Bool) â†’ (Î»(b : Bool) â†’ [ b, b ]) a

Î»(a : Bool) â†’ [ a, a ]
```

This is why Î²-reduction can interpret incomplete code (like `List/generate 10`)

# Î²-reduction can improve code comprehension

Î²-reduction eliminates indirection

For example, consider the `Natural/greaterThan` function from Dhall's Prelude:

* [`store.dhall-lang.org` - `Prelude-v21.1.0/Natural/greaterThan.dhall`](https://store.dhall-lang.org/Prelude-v21.1.0/Natural/greaterThan.dhall.html#var2-5)

. . .

That has the following dependency tree:

```
https://prelude.dhall-lang.org/Natural/greaterThan.dhall
â”” ./lessThan.dhall
  â”œ ../Bool/not.dhall
  â”” ./greaterThanEqual.dhall
    â”” ./lessThanEqual.dhall
```

â€¦ but you can eliminate all of the indirection by Î²-reducing the expression<sup>â€ </sup>:

```haskell
âŠ¢ https://prelude.dhall-lang.org/Natural/greaterThan.dhall


Î»(x : Natural) â†’ Î»(y : Natural) â†’ Natural/isZero (Natural/subtract y x) == False
```

<sup>â€ </sup> Technically, you also have to resolve imports before Î²-reducing the expression

# Generalizing evaluation

Morte and Dhall both "evaluate" an expression by:

* Î²-reducing the expression
* Pretty-printing the Î²-reduced expression

Carefully note that this "generalized evaluation" works for arbitrary
expressions

You're not limited to evaluating expressions that return plain/inert data

# More Î²-reduction examples

Here are some more examples of how powerful Î²-reduction is:

```haskell
âŠ¢ :let sum = https://prelude.dhall-lang.org/Natural/sum.dhall

âŠ¢ Î»(x : Natural) â†’ Î»(y : Natural) â†’ sum [ x, y ]  -- You can inline code

Î»(x : Natural) â†’ Î»(y : Natural) â†’ x + y
```

. . .

```haskell
âŠ¢ Î»(x : Bool) â†’ if x then True else False  -- You can do symbolic simplification

Î»(x : Bool) â†’ x
```

```haskell
âŠ¢ :paste
-- Entering multi-line mode. Press <Ctrl-D> to finish.
| :let Map =
|        Î»(args : { key : Type, value : Type }) â†’
|          List { mapKey : args.key, mapValue : args.value }
| 

âŠ¢ Map { key = Text, value = Bool }  -- You can Î²-reduce types

List { mapKey : Text, mapValue : Bool }
```

# Overview

* Î²-reduction
* **Name preservation**
* Namespaced De Bruijn indices
* Comparison to existing approaches

# Î²-reduction can preserve names

Here are some examples of how Î²-reduction can preserves names

```haskell
âŠ¢ Î»(x : Bool) â†’ x  -- Phew!  That was easy ğŸ˜Œ

Î»(x : Bool) â†’ x
```

. . .

```haskell
âŠ¢ Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(z : Bool) â†’ y) x  -- Still straightforward

Î»(x : Bool) â†’ Î»(z : Bool) â†’ x
```

```haskell
âŠ¢ :let compose = Î»(f : Bool â†’ Bool) â†’ Î»(g : Bool â†’ Bool) â†’ Î»(x : Bool) â†’ f (g x) 

compose : âˆ€(f : Bool â†’ Bool) â†’ âˆ€(g : Bool â†’ Bool) â†’ âˆ€(x : Bool) â†’ Bool

âŠ¢ compose (Î»(y : Bool) â†’ y) (Î»(z : Bool) â†’ z)  -- Tiny bit trickier

Î»(x : Bool) â†’ x
```

# The hard part

What should the interpreter return as the normal form for this expression?

```haskell
Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x
```

. . .

I'll give you a head start:

```haskell
Î»(? : Bool) â†’ Î»(? : Text) â†’ ?
```

# Name capture

I'll start off with the wrong answer.  If you Î²-reduce this:

```haskell
Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x
```

â€¦ you should **NOT** get this:

```haskell
Î»(x : Bool) â†’ Î»(x : Text) â†’ x
```

That last `x` refers to the second (`Text`) argument, but the correct answer
should return the first (`Bool`) argument

This is a common type of implementation error known as "name capture"
 
For example, the first draft of Morte made exactly this mistake:
 
* [Gabriel439/Morte - Issue #1](https://github.com/Gabriel439/Haskell-Morte-Library/issues/1)

â€¦ and the discussion on that issue is where I devised this trick

# Capture-avoiding substitution

A "capture-avoiding" substitution algorithm does not have "name capture" bugs

Capture-avoiding substitution algorithms typically fall into two categories:

* Named representations

  This approach preserves variable names, but adds some unique suffix to avoid
  name capture

* Nameless representations (i.e. De Bruijn indices)

  This approach replace variable names with integers

# De Bruijn indices

For example, using De Bruijn indices our pathological expression:

```haskell
Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x
```

â€¦ would Î²-reduce to:

```haskell
Î» â†’ Î» â†’ @1
```

â€¦ where `@n` denotes a De Bruijn index of `n`

The bug is gone, but now our original names are gone, too! ğŸ˜”

# Renaming variables

Another solution is add a unique suffix variables when their names collide

For example, such an implementation might Î²-reduce our pathological expression:

```haskell
Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x
```

â€¦ to:

```haskell
Î»(x : Bool) â†’ Î»(x1 : Bool) â†’ x
```

Henceforth, I will call this solution "name mangling"

# Name mangling in GHC

We can see what GHC does by saving our function to a Haskell file:

```haskell
module Example where

f :: Bool -> String -> Bool
f = \x -> (\y x -> y) x
```

â€¦ and asking GHC to dump an intermediate representation:

```haskell
$ ghc -O2 -ddump-simpl -dsuppress-all Example.hs
â€¦
f = \ x_atz _ -> x_atz
```

`ghc` did two things to avoid the name collision:

* rename the second variable to `_` since it's unused
* add a unique suffix to `x` to avoid collision

Even if GHC could pretty-print arbitrary expressions, the names would be mangled

# Mangling types

GHC doesn't support generalized evaluation, so name mangling is not as visible

**BUT**, name mangling still affects the user experience in another way: types!

Name mangling shows up in error messages:

```haskell
>>> :type ([] .)

<interactive>:1:2: error:
    â€¢ Couldn't match expected type â€˜b -> câ€™ with actual type â€˜[a0]â€™
    â€¢ In the first argument of â€˜(.)â€™, namely â€˜[]â€™
      In the expression: ([] .)
```

â€¦ and inferred types:

```
>>> :type (const .)
(const .) :: (a1 -> a2) -> a1 -> b -> a2
```

We get used to this, but polluting types with irrelevant numbers is jarring!

# Spoiler

Okay, but what does Dhall *actually* return for our pathological example?

```haskell
âŠ¢ Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x

Î»(x : Bool) â†’ Î»(x : Text) â†’ x@1
```

Dhall uses a representation that mixes a named and nameless representation

The upcoming sections will explain that in more detail

# Overview

* Î²-reduction
* Name preservation
* **Namespaced De Bruijn indices**
* Comparison to existing approaches

# Named representation

I will start with an untyped lambda calculus for this part to simplify things:

```haskell
data Syntax
    = Variable String
    | Lambda String Syntax
    | Apply Syntax Syntax
```

The above syntax tree is a "named" representation

For example, we would represent this lambda expression:

```haskell
Î»f â†’ Î»x â†’ f x
```

â€¦ as this `Syntax` tree:

```haskell
Lambda "f" (Lambda "x" (Apply (Variable "f") (Variable "x")))
```

I won't present the substitution algorithm for this approach (it's inelegant)

# Nameless representation

The corresponding nameless representation is:

```haskell
data Syntax
    = Variable Int
    | Lambda Syntax
    | Apply Syntax Syntax
```

For example, we would represent this lambda expression:

```haskell
Î»f â†’ Î»x â†’ f x
```

â€¦ as this `Syntax` tree:

```haskell
Lambda (Lambda (Apply (Variable 1) (Variable 0)))
```

â€¦ as if the user had written:

```haskell
Î» â†’ Î» â†’ @1 @0
```

I prefix the indices with `@` to avoid confusing them with ordinary numbers

# De Bruijn indices

A [De Bruijn index](https://en.wikipedia.org/wiki/De_Bruijn_index) replaces a
variable name with a number

The number counts how far away the matching lambda is:

```haskell
    â”Œâ”€â”€â”€â”€â”€â”€â”€â”
    â†“       â”‚
Î» â†’ Î» â†’ @1 @0
â†‘        â”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```

This variable numbering convention has the following nice properties:

* index assignment is context free
* this convention biases towards low indices (especially `0`)
* the capture-avoiding substitution algorithm is simple

# Substitution algorithm for De Bruijn indices

This is adapted from Pierce's "Types and Programming Languages" book:

```
k[j â‰” s]        =  | s  if j = k
                   | k  otherwise
(Î» â†’ t)[j â‰” s]  =  Î» â†’ t[j+1 â‰” â†‘(1,0)s]
(t u)[j â‰” s]    =  (t[j â‰” s] u[j â‰” s])

â†‘(d,j)k        =  | k+d  if j â‰¤ k
                  | k    otherwise
â†‘(d,j)(Î» â†’ t)  =  Î» â†’ â†‘(d,j+1)t
â†‘(d,j)(t u)    =  (â†‘(d,j)t â†‘(d,j)u)
```

I won't walk through this but I want people to appreciate the following:

* The algorithm is concise
* The algorithm doesn't require any sort of name supply or name mangling

I will include a Haskell algorithm at the end of this talk that's not so terse

# Î±-reduction

Now that we've introduced De Bruijn indices we can define "Î±-reduction":

> Î±-reduction converts an expression to the equivalent representation using De
> Bruijn indices

I've never seen this definition in the wild, but this is the "obvious"
definition

# Î±-equivalence

The reason I call this Î±-reduction is because you can define Î±-equivalence as
follows:

> Two terms are Î±-equivalent if their Î±-reduced forms are identical

For example, this expression:

```haskell
Î»x â†’ x
```

â€¦ and this expression:

```haskell
Î»y â†’ y
```

â€¦ are Î±-equivalent because they both share the same representation using De
Bruijn indices:

```haskell
Î» â†’ @0
```

This parallels the way Î²-equivalence is defined:

> Two terms are Î²-equivalent if their Î²-reduced forms are identical

â€¦ and that's why the definition I gave for Î±-reduction is the "obvious" one

# Comparing named and nameless

Let's compare and contrast the two representations:

```haskell
data Syntax                 â”‚  data Syntax
    = Variable String       â”‚      = Variable Int
    | Lambda String Syntax  â”‚      | Lambda Syntax
    | Apply Syntax Syntax   â”‚      | Apply Syntax Syntax
```

The nameless representation:

* Uses `Int` instead of `String` in the `Variable` constructor

* Omits the variable name for `Lambda`

# The trick

I could summarize the entire presentation with this meme:

![](./porque.jpg)

Translation: "Why not both?"

We can get the best of both worlds by mixing a named and nameless representation

However, there are multiple ways to do this and I have a specific way in mind

# Namespaced De Bruijn indices - Part 1

The first part of the trick is to use this syntax tree:

```haskell
data Syntax
    = Variable String Int
    | Lambda String Syntax
    | Apply Syntax Syntax
```

â€¦ which is a hybrid between the two representations we presented so far:

```haskell
data Syntax                 â”‚  data Syntax
    = Variable String       â”‚      = Variable Int
    | Lambda String Syntax  â”‚      | Lambda Syntax
    | Apply Syntax Syntax   â”‚      | Apply Syntax Syntax
```

This is like the named representation, except every variable has a De Bruijn
index

I call this representation "Namespaced De Bruijn indices"

# Example 0

The easiest way to explain this representation is with an example

The following expression:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ x@0
```

â€¦ corresponds to this syntax tree:

```haskell
Lambda "x" (Lambda "y" (Lambda "x" (Variable "x" 0)))
```

â€¦ and it represents a curried function that returns the third argument:

```haskell
           â”Œâ”€â”€â”€â”
           â†“   â”‚
Î»x â†’ Î»y â†’ Î»x â†’ x@0
```

This is pretty straightforward

# Example 1

Now let's try a slightly more interesting example

The following expression:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ y@0
```

â€¦ corresponds to this syntax tree:

```haskell
Lambda "x" (Lambda "y" (Lambda "x" (Variable "y" 0)))
```

â€¦ which returns the second function argument:

```haskell
      â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”
      â†“        â”‚
Î»x â†’ Î»y â†’ Î»x â†’ y@0
```

â€¦ because that is the innermost bound variable named `y`

Note: the De Bruijn index is `0`, but we ignore bound variables other than `y`

You can think of the index as being "namespaced" only to variables named `y`

# Example 2

Now let's try an even more interesting example

The following expression:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ x@1
```

â€¦ corresponds to this syntax tree:

```haskell
Lambda "x" (Lambda "y" (Lambda "x" (Variable "x" 1)))
```

â€¦ which returns the first function argument:

```haskell
 â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
 â†“             â”‚
Î»x â†’ Î»y â†’ Î»x â†’ x@1
```

â€¦ because that is the second-innermost bound variable named `x`

Like the last example, the index is namespaced to variables named `x`

This means that users can refer to shadowed variables using a non-zero index!

# Namespaced De Bruijn indices - Part 2

The second part of the trick is to add syntactic sugar for eliding 0 indices

In other words, this code:

```haskell
Î»x â†’ (Î»y â†’ Î»x â†’ y) x
```

â€¦ is syntactic sugar for this more explicit form:

```haskell
Î»x â†’ (Î»y â†’ Î»x â†’ y@0) x@0
```

â€¦ and both of them parse to the same syntax tree, which is:

```haskell
Lambda "x" (Apply (Lambda "y" (Lambda "x" (Variable "y" 0))) (Variable "x" 0))
```

This syntactic sugar also works in reverse when pretty-printing expressions

If you were to pretty-print the above syntax tree you would get:

```haskell
Î»x â†’ (Î»y â†’ Î»x â†’ y) x
```

â€¦ because the pretty-printer elides 0 indices

# Examples - Revisited

We can simplify this:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ x@0
```

â€¦ to this:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ x
```

Similarly, we can simplify this:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ y@0
```

â€¦ to this:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ y
```

But we cannot simplify this:

```haskell
Î»x â†’ Î»y â†’ Î»x â†’ x@1
```

# Namespaced indices in practice

This trick is neat because the syntactic sugar makes indices unintrusive

In practice, most users don't even know indices exist (Case in point: Dhall)

The reason why is because non-zero indices only arise in two cases:

* The user wishes to explicitly reference a shadowed variable (which is rare)

  â€¦ such as our last example:

  ```haskell
  Î»x â†’ Î»y â†’ Î»x â†’ x@1
  ```

* The indices appear in a Î²-reduced result (also rare)

  For example, our pathological input has no visible indices:

  ```haskell
  Î»x â†’ (Î»y â†’ Î»x â†’ y) x
  ```

  â€¦ but the Î²-reduced result requires a visible index:

  ```haskell
  Î»x â†’ Î»x â†’ x@1
  ```

# Substitution algorithm for Namespaced De Bruijn indices

Here's the original substution algorithm for De Bruijn indices:

```
k[j â‰” s]        =  | s  if j = k
                   | k  otherwise
(Î» â†’ t)[j â‰” s]  =  Î» â†’ t[j+1 â‰” â†‘(1,0)s]
(t u)[j â‰” s]    =  (t[j â‰” s] u[j â‰” s])

â†‘(d,j)k        =  | k+d  if j â‰¤ k
                  | k    otherwise
â†‘(d,j)(Î» â†’ t)  =  Î» â†’ â†‘(d,j+1)t
â†‘(d,j)(t u)    =  (â†‘(d,j)t â†‘(d,j)u)
```

â€¦ and here's how you update the algorithm for namespaced De Bruijn indices:

```
y@k[x@j â‰” s]       =  | s  if x = y && j = k
                      | k  otherwise
(Î»y â†’ t)[x@j â‰” s]  =  | Î»y â†’ t[x@(j+1) â‰” â†‘(1,x@0)s]  if x = y
                      | Î»y â†’ t[x@j     â‰”         s]  otherwise
(t u)[x@j â‰” s]     =  (t[x@j â‰” s] u[x@j â‰” s])

â†‘(d,x@j)y@k       =  | y@(k+d)  if x = y && j â‰¤ k
                     | y@k      otherwise
â†‘(d,x@j)(Î»y â†’ t)  =  | Î»y â†’ â†‘(d,x@(j+1))t  if x = y
                     | Î»y â†’ â†‘(d,x@j    )t  otherwise
â†‘(d,x@j)(t u)     =  (â†‘(d,x@j)t â†‘(d,x@j)u)
```

# Don't get hung up on the implementation

I want to stress that the implementation is not the important part here

There are more efficient solutions than the algorithm on the previous slide<sup>â€ </sup>

The key thing to take away from this talk is the **desired user experience**:

* We should let input code reference shadowed variables
* We should let output results reference shadowed variables

If we satisfy those requirements we benefit from the nice emergent properties

<sup>â€ </sup> Check out this project for a more efficient implementation you can fork:

* [GitHub - Fall-from-Grace](https://github.com/Gabriel439/grace)

# Variable shadowing - Bug or feature?

"Should we provide userland support for referencing shadowed variables?"  **YES!**

Consider this example code, which references a shadowed variable using an index:

```haskell
âŠ¢ :let f = Î»(x : Bool) â†’ Î»(x : Text) â†’ x@1

f : âˆ€(x : Bool) â†’ âˆ€(x : Text) â†’ Bool
```

If we Î·-expand that expression, the index disappears!

```haskell
âŠ¢ Î»(a : Bool) â†’ f a

Î»(a : Bool) â†’ Î»(x : Text) â†’ a
```

Indices intelligently revert to zero when they're no longer needed

What if you forgo language support in favor of mangling the name in userland?

```haskell
âŠ¢ :let f = Î»(x : Bool) â†’ Î»(x1 : Text) â†’ x

f : âˆ€(x : Bool) â†’ âˆ€(x1 : Text) â†’ Bool
```

Now the name is irreversibly scarred even after Î·-expansion:

```haskell
âŠ¢ Î»(a : Bool) â†’ f a

Î»(a : Bool) â†’ Î»(x1 : Text) â†’ a
```

# Overview

* Î²-reduction
* Name preservation
* Namespaced De Bruijn indices
* **Comparison to existing approaches**

# Comparison to name mangling - Part 1

"Aren't namespaced De Bruijn indices just another form of name mangling?" **NO!**

Consider this expression, which Î·-expands our pathological example:

```haskell
âŠ¢ Î»(a : Bool) â†’ (Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x) a

Î»(a : Bool) â†’ Î»(x : Text) â†’ a
```

Internally there is a name collision, but the final result requires no indices

A name mangling approach would get this wrong and produce something like:

```haskell
Î»(a : Bool) â†’ Î»(x1 : Text) â†’ a
```

Name mangling irreversibly scars variable names when avoiding name capture

Namespaced De Bruijn indices revert back to 0 when shadowing disappears

```haskell
âŠ¢ Î»(a : Bool) â†’ (Î»(x : Bool) â†’ Î»(x : Text) â†’ x@1) a

Î»(a : Bool) â†’ Î»(x : Text) â†’ a
```

# Comparison to name mangling - Part 2

This approach improves upon the type-level UX, too!

The inferred type of our pathological expression has no mangled names at all:

```haskell
âŠ¢ :type Î»(x : Bool) â†’ (Î»(y : Bool) â†’ Î»(x : Text) â†’ y) x

âˆ€(x : Bool) â†’ âˆ€(x : Text) â†’ Bool
```

Had we used name mangling then we would have gotten an inferred type like:

```haskell
âˆ€(x : Bool) â†’ âˆ€(x1 : Text) â†’ Bool
```

# Comparison to De Bruijn indices

Namespaced De Bruijn indices generalize traditional De Bruijn indices

You "Î±-reduce" namespaced DeBruijn indices by renaming all variables to `_`:

For example this, expression:

```haskell
Î»x â†’ Î»y â†’ x
```

â€¦ Î±-reduces to:

```haskell
Î»_ â†’ Î»_ â†’ _@1
```

The result is still using our namespaced De Bruijn index representation, though:

```haskell
Lambda "_" (Lambda "_" (Variable "_" 1))
```

We don't need separate syntax trees for our named and nameless representation!

# Conclusion

Namespaced De Bruijn indices combine the best of both worlds:

* We preserve names like a traditional named representation
* We get a mangling-free substitution algorithm like De Bruijn indices

This means that:

* We can pretty-print arbitrary Î²-reduced expressions
* We also get language support for referencing shadowed variables

This trick is most appropriate for languages that pretty-print Î²-reduced
expressions

This trick is useful for other interpreted languages, if only to simplify their
implementation

This approach has been vetted extensively in the wild via Dhall

# Appendix - Implementation

```haskell
-- | Syntax tree
data Syntax
    = Variable String Int
    | Lambda String Syntax
    | Apply Syntax Syntax
    deriving (Eq, Show)

{-| Increase the index of all bound variables matching the given variable name

    This is modified from the Shifting definition in Pierce's \"Types and
    Programming Languages\" by adding an additional argument for the namespace
    to shift
-}
shift
    :: Int
    -- ^ The amount to shift by
    -> String
    -- ^ The variable name to match (a.k.a. the namespace)
    -> Int
    -- ^ The minimum bound for which indices to shift
    -> Syntax
    -- ^ The expression to shift
    -> Syntax
shift offset namespace minIndex syntax =
    case syntax of
        Variable name index -> Variable name index'
          where
            index'
                | name == namespace && minIndex <= index = index + offset
                | otherwise                              = index

        Lambda name body -> Lambda name body'
          where
            minIndex'
                | name == namespace = minIndex + 1
                | otherwise         = minIndex

            body' = shift offset namespace minIndex' body

        Apply function argument -> Apply function' argument'
          where
            function' = shift offset namespace minIndex function

            argument' = shift offset namespace minIndex argument

{-| Substitute the given variable name and index with an expression

    This is modified from the Substitution definition in Pierce's \"Types and
    Programming Languages\" by adding an additional argument for the variable
    index
-}
substitute
    :: Syntax
    -- ^ The expression to substitute into
    -> String
    -- ^ The name of the variable to replace
    -> Int
    -- ^ The index of the variable to replace
    -> Syntax
    -- ^ The expression to substitute in place of the given variable
    -> Syntax
substitute expression name index replacement =
    case expression of
        Variable name' index'
            | name == name' && index == index' -> replacement
            | otherwise                        -> Variable name' index'

        Lambda name' body -> Lambda name' body'
          where
            index'
                | name == name' = index + 1
                | otherwise     = index

            shiftedBody = shift 1 name' 0 replacement

            body' = substitute body name index' shiftedBody

        Apply function argument -> Apply function' argument'
          where
            function' = substitute function name index replacement

            argument' = substitute argument name index replacement

-- | Î²-reduce an expression
betaReduce :: Syntax -> Syntax
betaReduce syntax =
    case syntax of
        Variable name index -> Variable name index

        Lambda name body -> Lambda name body'
          where
            body' = betaReduce body

        Apply function argument ->
            case function' of
                Lambda name body -> body'
                  where
                    shiftedArgument = shift 1 name 0 argument

                    substitutedBody = substitute body name 0 shiftedArgument

                    unshiftedBody = shift (-1) name 0 substitutedBody

                    body' = betaReduce unshiftedBody

                _ -> Apply function' argument'
          where
            function' = betaReduce function

            argument' = betaReduce argument

-- | Î±-reduce an expression
alphaReduce :: Syntax -> Syntax
alphaReduce syntax =
    case syntax of
        Variable name index -> Variable name index

        Lambda name body -> Lambda "_" body'
          where
            shiftedBody = shift 1 "_" 0 body

            substitutedBody = substitute shiftedBody name 0 (Variable "_" 0)

            unshiftedBody = shift (-1) name 0 substitutedBody

            body' = alphaReduce unshiftedBody

        Apply function argument -> Apply function' argument'
          where
            function' = alphaReduce function

            argument' = alphaReduce argument

-- | Returns `True` if the two input expressions are Î±-equivalent
alphaEquivalent :: Syntax -> Syntax -> Bool
alphaEquivalent left right = alphaReduce left == alphaReduce right
```

[dhall]: https://dhall-lang.org
